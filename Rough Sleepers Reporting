/*
TITLE: Rough Sleepers Team Reporting
REPORTING PERIOD: 01-OCT-2021 TO 31-DEC-2021
DESCRIPTION: 
    Calculates denominators and numerators for various KPIs including:
    - Wait times for first FTF/TM appointment (28-day target).
    - GP registration at discharge for patients with no GP at referral.
    - Accommodation status at discharge.
    - Caseload and assessment counts (HoNOS/DIALOG).
*/

DECLARE @START INT = 20211001; 
DECLARE @END   INT = 20211231;

SELECT 
    A.PATIENT_KEY,
    E.CLIENTID,
    A.REFERRAL_UNIQUE_ID,
    D.REFERRAL_SOURCE_DESC,
    D.REFERRAL_REASON_DESC,
    A.SERVICE_KEY,
    B.SERVICE_SSDESC,
    CONVERT(DATE, CAST(A.ALLOCATION_START_DATE_KEY AS VARCHAR(10))) AS TEAM_REFERRAL_START_DATE,
    CONVERT(DATE, CAST(A.ALLOCATION_END_DATE_KEY AS VARCHAR(10)))   AS TEAM_REFERRAL_END_DATE,
    C.QUARTER,
    C.YEAR_AND_MONTH AS REF_TRANSFER_YEAR_MONTH,
    CONVERT(DATE, CAST(D.REFERRAL_DATE_KEY AS VARCHAR(10)))         AS OVERALL_REFERRAL_START_DATE,
    CONVERT(DATE, CAST(D.DISCHARGE_DATE_KEY AS VARCHAR(10)))        AS OVERALL_REFERRAL_END_DATE,
    CONVERT(DATE, CAST(D.REJECTION_DATE_KEY AS VARCHAR(10)))        AS OVERALL_REJECTION_DATE,

    ------- QUESTION 1: Wait Times (28 Days) -------
    CASE
        WHEN (D.REJECTION_DATE_KEY IS NULL 
              AND RS1.APPOINTMENT_ID IS NOT NULL 
              AND A.ALLOCATION_START_DATE_KEY BETWEEN @START AND @END)
        THEN 'YES' ELSE 'NO'
    END AS [QUE 1 DENOMINATOR],

    CASE
        WHEN RS1.APPOINTMENT_ID IS NULL THEN 'NO APPT'
        WHEN (D.REJECTION_DATE_KEY IS NULL 
              AND RS1.APPOINTMENT_ID IS NOT NULL 
              AND A.ALLOCATION_START_DATE_KEY BETWEEN @START AND @END
              AND DATEDIFF(DD, CONVERT(DATE, CAST(A.ALLOCATION_START_DATE_KEY AS VARCHAR(10))), CONVERT(DATE, CAST(RS1.APPOINTMENT_DATE_KEY AS VARCHAR(10)))) <= 28)
        THEN 'YES' ELSE 'NO'
    END AS [QUE 1 NUMERATOR],

    DATEDIFF(DD, CONVERT(DATE, CAST(A.ALLOCATION_START_DATE_KEY AS VARCHAR(10))), CONVERT(DATE, CAST(RS1.APPOINTMENT_DATE_KEY AS VARCHAR(10)))) AS WAIT_DAYS,
    CONVERT(DATE, CAST(RS1.APPOINTMENT_DATE_KEY AS VARCHAR(10))) AS APPOINTMENT_DATE,

    ------- QUESTION 2: GP Registration -------
    RS2.COUNT_OF_APPT,
    D.REFERRING_GP,
    CASE
        WHEN (D.REJECTION_DATE_KEY IS NULL 
              AND A.ALLOCATION_END_DATE_KEY BETWEEN @START AND @END 
              AND D.REFERRING_GP IS NULL 
              AND RS2.COUNT_OF_APPT >= 3)
        THEN 'YES' ELSE 'NO'
    END AS [QUE 2 DENOMINATOR],

    CASE
        WHEN (D.REJECTION_DATE_KEY IS NULL 
              AND A.ALLOCATION_END_DATE_KEY BETWEEN @START AND @END
              AND D.REFERRING_GP IS NULL 
              AND RS2.COUNT_OF_APPT >= 3
              AND (A.ALLOCATION_END_DATE_KEY >= GP1.RECORD_START_DATE_KEY 
                   AND (A.ALLOCATION_END_DATE_KEY <= GP1.RECORD_END_DATE_KEY OR GP1.RECORD_END_DATE_KEY IS NULL)))
        THEN 'YES' ELSE 'NO'
    END AS [QUE 2 NUMERATOR],

    ------- QUESTION 3: Accommodation Status -------
    CASE
        WHEN (D.REJECTION_DATE_KEY IS NULL 
              AND A.ALLOCATION_END_DATE_KEY BETWEEN @START AND @END 
              AND RS2.COUNT_OF_APPT >= 3)
        THEN 'YES' ELSE 'NO'
    END AS [QUE 3 DENOMINATOR],

    CASE
        WHEN (D.REJECTION_DATE_KEY IS NULL 
              AND A.ALLOCATION_END_DATE_KEY BETWEEN @START AND @END 
              AND RS2.COUNT_OF_APPT >= 3 
              AND E.ACCOMMODATION_STATUS_CODE = '12')
        THEN 'YES' ELSE 'NO'
    END AS [QUE 3 NUMERATOR],
    E.ACCOMMODATION_STATUS_DESC,

    ------- QUESTION 6/7/8: Activity & Caseload -------
    CASE
        WHEN (D.REJECTION_DATE_KEY IS NULL 
              AND A.ALLOCATION_START_DATE_KEY <= @END 
              AND (A.ALLOCATION_END_DATE_KEY >= @START OR A.ALLOCATION_END_DATE_KEY IS NULL))
        THEN 'YES' ELSE 'NO'
    END AS [QUE 6 NUMBER],

    CASE
        WHEN (D.REJECTION_DATE_KEY IS NULL AND A.ALLOCATION_START_DATE_KEY BETWEEN @START AND @END)
        THEN 'YES' ELSE 'NO'
    END AS [QUE 7 NUMBER],

    CASE
        WHEN (RS3.APPOINTMENT_ID IS NOT NULL AND A.ALLOCATION_START_DATE_KEY BETWEEN @START AND @END)
        THEN 'YES' ELSE 'NO'
    END AS [QUE 8 NUMERATOR],

    ------- QUESTION 9/10: Clinical Outcomes -------
    RS4.COUNT_OF_APPT AS TOTAL_FTF_TM_COUNT,
    CASE
        WHEN (A.ALLOCATION_START_DATE_KEY <= @END 
              AND (A.ALLOCATION_END_DATE_KEY >= @START OR A.ALLOCATION_END_DATE_KEY IS NULL)
              AND D.REJECTION_DATE_KEY IS NULL 
              AND RS4.COUNT_OF_APPT >= 3)
        THEN 'YES' ELSE 'NO'
    END AS [QUE 9 NUMERATOR],

    ASMNT.COUNT_OF_ASSESSMENT,
    CASE
        WHEN (D.REJECTION_DATE_KEY IS NULL 
              AND (A.ALLOCATION_START_DATE_KEY <= @END AND (A.ALLOCATION_END_DATE_KEY >= @START OR A.ALLOCATION_END_DATE_KEY IS NULL))
              AND ASMNT.COUNT_OF_ASSESSMENT >= 1)
        THEN 'YES' ELSE 'NO'
    END AS [QUE 10 NUMERATOR],

    ------- QUESTION 11/12: Discharge Logic -------
    CASE 
        WHEN (A.ALLOCATION_END_DATE_KEY BETWEEN @START AND @END AND A.ALLOCATION_END_DATE_KEY = D.DISCHARGE_DATE_KEY) THEN 'DISCHARGED'
        WHEN (A.ALLOCATION_END_DATE_KEY BETWEEN @START AND @END AND A.ALLOCATION_END_DATE_KEY <> D.DISCHARGE_DATE_KEY) THEN 'TRANSFERRED'
        ELSE 'NOT DISCHARGED IN PERIOD'  
    END AS [QUESTION 11 NUMBER],

    D.DISCHARGE_REASON_DESC,
    CASE
        WHEN (A.ALLOCATION_END_DATE_KEY BETWEEN @START AND @END AND D.DISCHARGE_REASON_CODE IN ('01','08')) THEN 'ONWARD REFERRAL' 
        ELSE 'OTHER/NOT DISCHARGED'
    END AS [QUE 12 NUMERATOR]

FROM FACT_REFERRAL_SERVICE A 
LEFT JOIN DIM_SERVICE B ON A.SERVICE_KEY = B.SERVICE_KEY AND B.RECORD_STATUS = 'A'
LEFT JOIN DIM_DATE C ON A.ALLOCATION_START_DATE_KEY = C.DATE_KEY 
LEFT JOIN FACT_REFERRAL D ON A.PATIENT_KEY = D.PATIENT_KEY AND A.REFERRAL_ID = D.REFERRAL_ID
LEFT JOIN DIM_PATIENT E ON A.PATIENT_KEY = E.PATIENT_KEY AND E.RECORD_STATUS = 'A'

-- RS1: Earliest FTF/TM Appointment after Team Referral
OUTER APPLY (
    SELECT TOP (1) APPOINTMENT_ID, APPOINTMENT_DATE_KEY 
    FROM FACT_APPOINTMENT APT1
    WHERE APT1.PATIENT_KEY = A.PATIENT_KEY    
      AND APT1.CAL_OUTCOME_DESC IN ('FTF','TM')
      AND APT1.SERVICE_KEY = A.SERVICE_KEY 
      AND APT1.HCP_TYPE = 'Lead'
      AND APT1.APPOINTMENT_DATE_KEY >= A.ALLOCATION_START_DATE_KEY
    ORDER BY APPOINTMENT_DATE_KEY ASC
) RS1 

-- RS2: Count of FTF/TM/TC prior to discharge
OUTER APPLY (
    SELECT COUNT(*) AS COUNT_OF_APPT
    FROM FACT_APPOINTMENT APT1
    WHERE APT1.PATIENT_KEY = A.PATIENT_KEY    
      AND APT1.CAL_OUTCOME_DESC IN ('FTF','TM','TC')
      AND APT1.HCP_TYPE = 'Lead'
      AND APT1.SERVICE_KEY = A.SERVICE_KEY
      AND APT1.APPOINTMENT_DATE_KEY <= A.ALLOCATION_END_DATE_KEY
) RS2

-- RS3: Checking for any FTF/TM after Referral
OUTER APPLY (
    SELECT TOP (1) APPOINTMENT_ID
    FROM FACT_APPOINTMENT APT1
    WHERE APT1.PATIENT_KEY = A.PATIENT_KEY    
      AND APT1.CAL_OUTCOME_DESC IN ('FTF','TM')
      AND APT1.SERVICE_KEY = A.SERVICE_KEY 
      AND APT1.APPOINTMENT_DATE_KEY >= A.ALLOCATION_START_DATE_KEY 
) RS3

-- RS4: Total FTF/TM count for the referral spell
OUTER APPLY (
    SELECT COUNT(*) AS COUNT_OF_APPT
    FROM FACT_APPOINTMENT APT1 
    WHERE APT1.PATIENT_KEY = A.PATIENT_KEY    
      AND APT1.CAL_OUTCOME_DESC IN ('FTF','TM')
      AND APT1.HCP_TYPE = 'Lead'
      AND APT1.SERVICE_KEY = A.SERVICE_KEY 
      AND APT1.APPOINTMENT_DATE_KEY >= A.ALLOCATION_START_DATE_KEY 
) RS4

-- ASMNT: Count of HoNOS or DIALOG assessments
OUTER APPLY (
    SELECT COUNT(*) AS COUNT_OF_ASSESSMENT
    FROM FACT_ASSESSMENT A1
    WHERE A1.PATIENT_KEY = A.PATIENT_KEY 
      AND A1.SS_CODE = A.SS_CODE
      AND (A1.ASSESSMENT_DATE_KEY >= A.ALLOCATION_START_DATE_KEY 
           AND (A1.ASSESSMENT_DATE_KEY <= A.ALLOCATION_END_DATE_KEY OR A.ALLOCATION_END_DATE_KEY IS NULL))
      AND (A1.ASSESSMENT_NAME LIKE '%HONOS%' OR A1.ASSESSMENT_NAME LIKE '%DIALOG%')
) ASMNT

-- GP1: Patient GP details during the referral spell
OUTER APPLY (
    SELECT TOP (1) GP_CODE, RECORD_START_DATE_KEY, RECORD_END_DATE_KEY
    FROM DIM_PATIENT PAT1
    WHERE PAT1.PATIENT_KEY = A.PATIENT_KEY
      AND PAT1.SS_CODE = A.SS_CODE
      AND (A.ALLOCATION_START_DATE_KEY <= [RECORD_END_DATE_KEY] OR RECORD_END_DATE_KEY IS NULL)
      AND (A.ALLOCATION_END_DATE_KEY IS NULL OR A.ALLOCATION_END_DATE_KEY >= [RECORD_START_DATE_KEY])
) GP1

WHERE A.SERVICE_KEY IN (540, 541, 544)
  AND E.DELETED_FLAG = 0
  AND E.TEST_PATIENT_FLAG = 0
  AND E.PATIENT_FLAG = 1
  AND E.LOGIC_DELETE = 'No';
